*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends


.options savecurrents


Vcc vcc 0 5.0
Vee vee 0 -5.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)

X1 inv_in 3 vcc vee out1 uA741

R1 0 inv_in 1000
R2 3 out1 150000
R3 0 3 1000
R4 out1 out 1000


C1 in inv_in 220e-9
C2 out 0 110e-9

.op
.end


.control

echo "tablestart_TAB"

print all

echo "tablestart_END"

* time analysis
tran 1e-5 1e-2
plot v(out)
hardcopy vo1.ps v(out)
echo vo1_FIG


* frequency analysis
ac dec 10 10 100MEG
plot vdb(out)

hardcopy gaint.ps vdb(out)
echo gaint_FIG

plot vp(out)
hardcopy phase.ps 180/pi*vp(out)
echo phase_FIG

meas ac max MAX vdb(out) from=10 to=100MEG

*meas ac max1 MAX v(out) from=10 to=100MEG

let y = max-3
meas ac lowcutoff WHEN vdb(out) = y CROSS=1
meas ac highcutoff WHEN vdb(out) = y CROSS=LAST
let gain = max
let centralfrequency = sqrt(highcutoff*lowcutoff)

let cost = 300+3+0.66+1.3323e4
let max2 = 10^(max/20)

let merit = 1/(cost*(abs(max2-100)+abs(1000-centralfrequency)+1e-6))
print max2

print centralfrequency
print merit


echo "tablegain_TAB"
print gain
echo "tablegain_END"

echo "tablefrequencies_TAB"

print lowcutoff
print highcutoff
print centralfrequency

echo "tablefrequencies_END"



echo "tableall_TAB"

print gain
print centralfrequency
print cost
print merit

echo "tableall_END"

*input impedance in kohm



let ZI1 = (-v(in)[20])/(i(vin)[20])
let zi_re = Re(ZI1)
let zi_im = Im(ZI1)


echo "tablezi_TAB"
echo "zi = $&zi_re $&zi_im j"
echo "tablezi_END"


*need a different setup to measure output impedance


.endc 


