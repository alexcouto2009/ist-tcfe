.options savecurrents

* PHILIPS BJT'S
.MODEL BC557A PNP(IS=2.059E-14 ISE=2.971f ISC=1.339E-14 XTI=3 BF=227.3 BR=7.69 IKF=0.08719 IKR=0.07646 XTB=1.5 VAF=37.2 VAR=11.42 VJE=0.5912 VJC=0.1 RE=0.688 RC=0.6437 RB=1 RBM=1 IRB=1E-06 CJE=1.4E-11 CJC=1.113E-11 XCJC=0.6288 FC=0.7947 NF=1.003 NR=1.007 NE=1.316 NC=1.15 MJE=0.3572 MJC=0.3414 TF=7.046E-10 TR=1m2 ITF=0.1947 VTF=5.367 XTF=4.217 EG=1.11)
.MODEL BC547A NPN(IS=1.533E-14 ISE=7.932E-16 ISC=8.305E-14 XTI=3 BF=178.7 BR=8.628 IKF=0.1216 IKR=0.1121 XTB=1.5 VAF=69.7 VAR=44.7 VJE=0.4209 VJC=0.2 RE=0.6395 RC=0.6508 RB=1 RBM=1 IRB=1E-06 CJE=1.61E-11 CJC=4.388p XCJC=0.6193 FC=0.7762 NF=1.002 NR=1.004 NE=1.436 NC=1.207 MJE=0.3071 MJC=0.2793 TF=4.995E-10 TR=1m2 ITF=0.7021 VTF=3.523 XTF=139 EG=1.11)

Vcc vcc 0 12.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)
Rin in in2 100

* input coupling capacitor
Ci in2 base 0.5m

* bias circuit
R1 vcc base 80k
R2 base 0 20k

* gain stage
Q1 coll base emit BC547A
Rc vcc coll 0.9k
Re emit 0 100

* bypass capacitor
Cb emit 0 3.5m


* output stage
Q2 0 coll emit2 BC557A
Rout emit2 vcc 1000

* output coupling capacitor
Co emit2 out 2m

* load
RL out 0 8


.control
op

echo "tablestart_TAB"
print all
echo "tablestart_END"

*------BJTs operating in F.A.R-------
let Vce = v(coll)-v(emit)
let Vbe = v(base)-v(emit)

let Vec = v(emit2)
let Veb = v(emit2)-v(coll)

echo "tableop_TAB"
print Vce-Vbe
print Vec-Veb
echo "tableop_END"
*------------------------------------


* time analysis
tran 1e-5 1e-2
*plot v(coll)
hardcopy vo1.ps vdb(coll)
echo vo1_FIG

* frequency analysis
ac dec 10 10 100MEG
*plot vdb(coll)
hardcopy vo1f.ps vdb(coll)
echo vo1f_FIG


*plot vdb(out)
hardcopy vo2f.ps vdb(out)
echo vo2f_FIG


let gain_stage_1 = vdb(coll)-vdb(in)
*plot gain_stage_1
hardcopy gain1f.ps gain_stage_1
echo gain1f_FIG

let gain_stage_2 = vdb(out)-vdb(coll)
*plot gain_stage_2
hardcopy gain2f.ps gain_stage_2
echo gain2f_FIG

let gain_total = vdb(out)-vdb(in)
*plot gain_total
hardcopy gaintf.ps gain_total
echo gaintf_FIG

hardcopy teste.ps vdb(in)


*input impedance in kohm

let zi_re = re(v(in2)[40]/i(Vin)[40])
let zi_im = im(v(in2)[40]/i(Vin)[40])

echo "tablezi_TAB"
echo "zi = $&zi_re + $&zi_im j"
echo "tablezi_END"

hardcopy in_imp.ps -v(in2)/i(Vin)
echo in_imp_FIG


*need a different setup to measure output impedance
*echo "tablezo_TAB"

*echo "tablezo_END"

*let routimp = v(coll)[40]/v(out)[40]*8-8

*let outimp = 1/(1/routimp-1/8)
*print routimp
*print outimp
*--------------------------------------------------

meas ac max MAX vdb(out) from=10 to=100MEG

let y = max-3

meas ac lowcutoff WHEN vdb(out) = y CROSS=1

meas ac highcutoff WHEN vdb(out) = y CROSS=LAST
let bandwidth = highcutoff - lowcutoff

echo "tableband_TAB"
*print max
print lowcutoff
print highcutoff
print bandwidth
echo "tableband_END"



meas ac gain2 AVG vdb(out) from=lowcutoff to=highcutoff

let gaint=abs(gain2)

let cost=0.2+0.1+100+2+500+3500+2000+0.008

let merit= gaint*bandwidth/lowcutoff/cost

echo "tablefinal_TAB"
print gaint
print lowcutoff
print bandwidth
print cost
print merit
echo "tablefinal_END"

quit
.endc 
.end
